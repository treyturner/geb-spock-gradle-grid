plugins {
    // https://github.com/erdi/idea-gradle-plugins
    id "com.github.erdi.idea-base" version "2.2"
    id "com.github.erdi.extended-idea" version "2.2"
}

allprojects {
    apply plugin: 'idea'

    group = 'info.treyturner.geb.demo'

    repositories {
	    mavenCentral()
    }

    tasks.withType(Test).configureEach {
        testLogging { // Log these test events to console
            events "started", "skipped", "passed", "failed", "standardOut"
        }
        outputs.upToDateWhen { false } // Always run tests

        /**
         * Parallel execution by is disabled by default. Uncomment one of the following to enable:
         */
        // Some OS and CPUs report twice as many cores due to HyperThreading, so we use:
        //maxParallelForks(Runtime.runtime.availableProcessors().intdiv(2))
        // Others report the correct number of physical cores, so we simply use:
        //maxParallelForks(Runtime.runtime.availableProcessors())
    }
}

subprojects {
    apply plugin: 'groovy'

    dependencies {
        // Groovy
        implementation 'org.apache.groovy:groovy:4.0.22'
        implementation 'org.apache.groovy:groovy-json:4.0.22'

        // Spock
        implementation 'org.spockframework:spock-core:2.3-groovy-4.0'

        // Spock Reports
        testImplementation('com.athaydes:spock-reports:2.5.1-groovy-4.0') {
            transitive = false // this avoids affecting your version of Groovy/Spock
        }

        // Logging via slf4j & logback
        implementation 'org.slf4j:slf4j-api:2.0.13'
        implementation 'ch.qos.logback:logback-core:1.5.6'
        implementation 'ch.qos.logback:logback-classic:1.5.6'
    }
}
